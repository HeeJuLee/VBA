Option Explicit

Dim isKeyworldSearch As Boolean
Dim isKeyworldSearch2 As Boolean
Dim searchResultCount As Long
Public isEnd As Boolean
Dim isFilterOn As Boolean
Dim bDelivery, bReceiving, bWorking As Boolean
Dim bEnableEvent As Boolean
Dim b3970, b8358 As Boolean

'최초 로드 시
Private Sub Worksheet_Activate()
    Dim i As Long
    Dim Y As Long
    
    Y = Year(Date)
    
    Me.cboSelectYear.List = Array("전체")
    Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    
    For i = 0 To Y - 2005
        Me.cboSelectYear.AddItem Y - i & "년"
    Next
    
    Me.txtFromDate.Visible = False
    Me.txtToDate.Visible = False
    Me.txtFromDueDate.Visible = False
    Me.txtToDueDate.Visible = False
    
    isKeyworldSearch = False
    isKeyworldSearch2 = False
    isEnd = False
    bEnableEvent = True
End Sub

'발주 데이터를 검색해서 시트에 출력
Sub OrderSearch()
    Dim db As Variant
    Dim pos, Y, M, D As Long
    Dim startTime As Single
    Dim endTime As Single
    
    On Error GoTo safe_exit_orderSearch
    
'    startTime = Timer
    
    '필터가 걸려있으면 지움
'    If isFilterOn Then
'        ToggleFilter
'    End If

    '기간 체크
    If optLast2Year.value = True Then
        txtFromDate.value = DateAdd("yyyy", -2, Date)
        txtToDate.value = ""
    Else
        If Me.cboSelectYear.value = "전체" Then
            txtFromDate.value = ""
            txtToDate.value = ""
        Else
            pos = InStr(Me.cboSelectYear.value, "년")
            If pos <> 0 Then
                Y = Left(Me.cboSelectYear.value, pos - 1)
            Else
                Y = Year(Date)
            End If
            
            If Me.cboSelectMonth.value = "전체" Then
                txtFromDate.value = DateSerial(Y, 1, 1)
                txtToDate.value = DateSerial(Y, 12, 31)
            Else
                pos = InStr(Me.cboSelectMonth.value, "월")
                If pos <> 0 Then
                    M = Left(Me.cboSelectMonth.value, pos - 1)
                Else
                    M = month(Date)
                End If
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, -1)
            End If
        End If
    End If
    
    '납기일자 체크
    Y = Year(Date)
    M = month(Date)
    D = day(Date)
    
    If optDueDateAll = True Then
        txtFromDueDate.value = ""
        txtToDueDate.value = ""
    ElseIf optDueDate1Week = True Then
        txtFromDueDate.value = DateSerial(Y, M, D - 7)
        txtToDueDate.value = DateSerial(Y, M, D + 7)
    ElseIf optDueDate2Week = True Then
        txtFromDueDate.value = DateSerial(Y, M, D - 7)
        txtToDueDate.value = DateSerial(Y, M, D + 14)
    ElseIf optDueDate1Month = True Then
        txtFromDueDate.value = DateSerial(Y, M, D - 7)
        txtToDueDate.value = DateSerial(Y, M + 1, D)
    End If
    
    '발주 데이터를 읽어옴
    db = Get_DB(shtOrder, False, False)
      
    If Not isEmpty(db) Then
        If txtFromDate.value <> "" Then db = Filtered_DB(db, ">=" & txtFromDate.value, 26)    '등록일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If txtToDate.value <> "" Then db = Filtered_DB(db, "<=" & txtToDate.value, 26)    '등록일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If txtFromDueDate.value <> "" Then db = Filtered_DB(db, ">=" & txtFromDueDate.value, 17)    '납기일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If txtToDueDate.value <> "" Then db = Filtered_DB(db, "<=" & txtToDueDate.value, 17)    '납기일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If btnWorking.value = True Then
            db = Get_Working_DB(db)
        ElseIf btnReceiving.value = True Then
            db = Get_Receiving_DB(db)   '입고대상: 발주값이 있고 입고값이 없는 경우
        ElseIf btnDelivery.value = True Then
            db = Get_Delivery_DB(db)   '납품대상: 수주값이 있고 납품값이 없는 경우
        End If
    End If
    If Not isEmpty(db) Then
        If btn3970.value = True Then
            db = Filtered_DB(db, "3970", 24)    '결제수단 필드와 비교
            If Not isEmpty(db) Then
                db = Filtered_DB_Empty(db, 22, True)    '결제값이 없는 것
            End If
        End If
        If btn8358.value = True Then
            db = Filtered_DB(db, "8358", 24)    '결제수단 필드와 비교
            If Not isEmpty(db) Then
                db = Filtered_DB_Empty(db, 22, True)    '결제값이 없는 것
            End If
        End If
    End If
    
    If Not isEmpty(db) Then
        If Me.txtKeyword.value <> "" Then db = Filtered_DB(db, Me.txtKeyword.value)     '모든 필드에서 키워드 검색
        If Me.txtKeyword2.value <> "" Then db = Filtered_DB(db, Me.txtKeyword2.value)     '모든 필드에서 키워드 검색
    End If
    
'    endTime = Timer
'    ActiveSheet.Range("U3").Value = Format(endTime - startTime, "#0.00")
    
    '시트 보호 해제
    'ActiveSheet.Unprotect
        
    '화면 변경 전에 Event 끄기  (sheet change event로 안들어가도록 함)
    Application.EnableEvents = False
    
    '기존 검색결과를 화면에서 지움
    ClearOrderSearchResult
    
    '검색결과를 화면에 출력
    If isEmpty(db) Then
        searchResultCount = 0     '검색결과 건수는 0
        
        isEnd = False
        Range("I6").Select
    Else
        '검색결과 건수를 저장
        searchResultCount = UBound(db, 1)
        
        '검색결과 라인 서식 적용
        SetContentsLine shtOrderAdmin.Range("B6"), "AC", searchResultCount
        
        '순번 출력
        SequenceToRng shtOrderAdmin.Range("C6"), UBound(db, 1)
        '검색결과 출력
        ArrayToRng shtOrderAdmin.Range("B6"), db, "1, ,28,5,3,4,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,31,26,27"
        '메모 출력
        SetComment db, 29, shtOrderAdmin.Range("J6")
        
        '관리번호/품명에 하이퍼링크
        'SetHyperLink2 db, 5, 7, shtOrderAdmin.Range("E6"), shtOrderAdmin.Range("I6")
        
        '수주 색상 적용
        SetContentsColor shtOrderAdmin.Range("B6"), "AC", db, 4, "수주", shtOrderAdmin.Range("B3").Interior.color
        
        '입고 예정 색상 적용
        If bWorking = True Or bReceiving = True Then
            SetReceivingColor shtOrderAdmin.Range("T6"), db, 16, 18, shtOrderAdmin.Range("B4").Interior.color
        End If
        
        If Me.btnDelivery Or Me.btnReceiving Or Me.btnWorking Or btn3970 Or btn8358 Then
            GoToFront
        Else
            isEnd = False
            GoToEnd
        End If
    End If
    
     '검색 결과 건수 출력
    Me.lblSearchResult.Caption = "(" & searchResultCount & "건)"
    
    '시트 보호
    'ActiveSheet.Protect DrawingObjects:=False, Contents:=True, Scenarios:= _
        True, AllowFormattingCells:=True, AllowFormattingColumns:=True, _
        AllowFormattingRows:=True, AllowFiltering:=True
        
safe_exit_orderSearch:
    bEnableEvent = True
    Application.EnableEvents = True
    
End Sub

'발주 검색결과를 화면에서 지움
Private Sub ClearOrderSearchResult()
    
    '아래쪽 라인 서식 지움
    ClearContentsLine shtOrderAdmin.Range("B6"), "AC"
    
    '3번 순번 열을 기준으로 지움
    ClearContentsBelow shtOrderAdmin.Range("B6"), "AC"
    
End Sub

'발주삭제
Sub OrderDelete()
    Dim cRow As Long
    Dim yn As VbMsgBoxResult
    
    '선택한 행 번호
    cRow = Selection.row

    '데이터가 있는 행이 아닐 경우는 중지
    If cRow < 6 Or Range("B" & cRow).value = "" Then
        MsgBox "삭제할 행을 선택하신 후 '삭제' 버튼을 클릭하세요."
        End
    End If

    '안내 문구 출력
    yn = MsgBox("선택한 발주정보를 정말로 삭제하시겠습니까?" & vbCrLf & "삭제한 정보는 복구가 불가능합니다.", vbYesNo)
    If yn = vbNo Then Exit Sub
    
    'DB에서 견적정보 삭제
    Delete_Record shtOrder, Range("B" & cRow).value

    OrderSearch
End Sub

'발주등록 폼 열기
Sub OpenOrderInsertForm()
    frmOrderInsert.Show (False)
End Sub


'발주수정 폼 열기
Sub OpenOrderUpdateForm()
    frmOrderUpdate.Show (False)
End Sub


''조회 기간', '검색어' 등 일반 텍스트 박스 클릭 시 Object 선택이 안되도록 클릭이벤트를 빈 매크로로 연결
Sub EmptyProcedure()

End Sub

Sub DeleteKeyword1()
    Me.txtKeyword.value = ""
    OrderSearch
End Sub

Sub DeleteKeyword2()
    Me.txtKeyword2.value = ""
    OrderSearch
End Sub

Sub GoToFront()
    With shtOrderAdmin
        .Activate
        .Range("I6").Select
    End With
End Sub

Sub GoToEnd()
    Dim endRow As Long
    
    With shtOrderAdmin
        .Activate
        If isEnd = False Then
            .Range("I" & .Range("B5").End(xlDown).row).Select
            isEnd = True
        Else
            .Range("I6").Select
            isEnd = False
        End If
    End With
End Sub


Sub ToggleFilter()
    With shtOrderAdmin
        Range("Q5:U" & .Range("D5").End(xlDown).row).Select
        Selection.AutoFilter
        Range("Q6").Select
    End With
        
    If isFilterOn = True Then isFilterOn = False Else isFilterOn = True
End Sub


'년 선택
Private Sub cboSelectYear_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        Me.cboSelectMonth.value = "전체"
        Me.optAll.value = True
    End If
End Sub

'월 선택
Private Sub cboSelectMonth_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        Me.cboSelectMonth.value = "전체"
    End If
End Sub

'전체 선택 시 '월'만 전체로 바꿈
Private Sub optAll_Click()
    Me.cboSelectMonth.value = "전체"
End Sub

'조회기간 - 이번달
Private Sub optThisMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    Me.cboSelectYear.value = Y & "년"
    Me.cboSelectMonth.value = M & "월"
End Sub

'조회기간 - 지난달
Private Sub optLastMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    If M = 1 Then
        Me.cboSelectYear.value = Y - 1 & "년"
        Me.cboSelectMonth.value = "12월"
    Else
        Me.cboSelectYear.value = Y & "년"
        Me.cboSelectMonth.value = M - 1 & "월"
    End If
End Sub

Private Sub btnDelivery_Click()
    If bEnableEvent = False Then
        Exit Sub
    End If
    
    bEnableEvent = False
    btnWorking.value = False
    btnReceiving.value = False
    bEnableEvent = True
End Sub

Private Sub btnReceiving_Click()
    If bEnableEvent = False Then
        Exit Sub
    End If
    
    bEnableEvent = False
    btnWorking.value = False
    btnDelivery.value = False
    bEnableEvent = True
End Sub

Private Sub btnWorking_Click()
    If bEnableEvent = False Then
        Exit Sub
    End If
    
    bEnableEvent = False
    btnReceiving.value = False
    btnDelivery.value = False
    bEnableEvent = True
End Sub

Private Sub cboSelectMonth_DropButtonClick()
    If Me.cboSelectMonth.LineCount = 0 Then
        Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    End If
End Sub

Private Sub cboSelectYear_DropButtonClick()
    Dim i As Long
    Dim Y As Long
    
    If Me.cboSelectYear.ListCount = 0 Then
        Me.cboSelectYear.List = Array("전체")
        
        Y = Year(Date)
        For i = 0 To Y - 2005
            Me.cboSelectYear.AddItem Y - i & "년"
        Next
    End If
End Sub

'카드결제 체크 - 3970
Private Sub btn3970_click()
    If bEnableEvent = False Then
        Exit Sub
    End If
    
    bEnableEvent = False
    btn8358.value = False
    bEnableEvent = True
End Sub

'카드결제 체크 - 8358
Private Sub btn8358_click()
    If bEnableEvent = False Then
        Exit Sub
    End If
    
    bEnableEvent = False
    btn3970.value = False
    bEnableEvent = True
End Sub

'키워드 검색 입력 시
Private Sub txtKeyword_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch = True
        OrderSearch
        isKeyworldSearch = False
    ElseIf KeyCode = 9 Then
        Me.txtKeyword2.Activate
    End If
End Sub

'재검색 입력 시
Private Sub txtKeyword2_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch2 = True
        OrderSearch
        isKeyworldSearch2 = False
    End If
End Sub

Private Sub Worksheet_BeforeDoubleClick(ByVal Target As Range, Cancel As Boolean)
    Dim cRow, cCol As Long

    '선택한 행 번호
    'cRow = Selection.row
    cRow = Target.row
    cCol = Target.Column
    
    '데이터가 있는 행이 아닐 경우는 중지
    If cRow < 6 Or shtOrderAdmin.Range("B" & cRow).value = "" Then
        End
    End If
    
    If cCol = 5 Then
        '관리번호를 더블클릭한 경우 관리번호로 검색
        Me.txtKeyword.value = Cells(cRow, 5).value
        If Me.cboSelectYear.value <> "전체" Then
            Me.cboSelectYear.value = "전체"
        Else
            Me.txtFromDate.value = ""
            Me.txtToDate.value = ""
            OrderSearch
        End If
    ElseIf cCol = 9 Then
        '더블클릭해서 셀 편집 모드가 안되도록 선택 셀을 다른 곳으로 옮겨줌
        Range("D" & cRow).Select
        
        '수주 데이터를 클릭한 경우에는 EstimateUpdate 폼을 띄움, 그렇지 않은 경우는 발주 수정 화면 띄움
        If shtOrderAdmin.Range("G" & cRow).value = "수주" Then
            
            If shtOrderAdmin.Range("D" & cRow).value = "" Then
                MsgBox "견적ID가 없습니다. 수주 데이터가 맞는지 확인이 필요합니다.", vbInformation, "작업 확인"
                
                If isFormLoaded("frmOrderUpdate") Then
                    Unload frmOrderUpdate
                End If
                frmOrderUpdate.Show (False)
            Else
                clickEstimateId = shtOrderAdmin.Range("D" & cRow).value
                
                If isFormLoaded("frmEstimateUpdate") Then
                    Unload frmEstimateUpdate
                End If
                frmEstimateUpdate.Show (False)
            End If
        Else
            If isFormLoaded("frmOrderUpdate") Then
                Unload frmOrderUpdate
            End If
            frmOrderUpdate.Show (False)
        End If
    End If
End Sub



Sub UpdateLastPayment(estimateId, managementId, fieldName, value)
    Dim db As Variant
    Dim lastId As Variant
    
    '만약 EstimateUpdate 폼에서 결제이력을 삭제한 경우에는 아래 진행하지 않음
    If bDeleteFlag = True Then
        Exit Sub
    End If
    
    db = Get_DB(shtPayment)
    db = Filtered_DB(db, estimateId, 2, True)
    If isEmpty(db) Then
        '기존 결제이력이 없으면 결제이력을 하나 추가함
        Insert_Record shtPayment, estimateId, managementId, , , , , , , , , Date
        lastId = Get_LastID(shtPayment)
    Else
        '결제이력이 있으면 맨 마지막 결제이력에 값을 업데이트함
        lastId = db(UBound(db), 1)
    End If
    
    Update_Record_Column shtPayment, lastId, fieldName, value
End Sub

Sub AppendShtOrder(orderId)
    Dim order As Variant
    Dim id, lastRow, row, num As Long
    
    On Error GoTo safe_exit_appendShtOrder
    
    order = Get_Record_Array(shtOrder, orderId)
    If isEmpty(order) Then
        MsgBox orderId & "에 해당하는 발주 데이터가 없습니다.", vbInformation, "작업 확인"
        Exit Sub
    End If
    
    '시트 맨 아래에 라인 추가
    lastRow = Range("B7").End(xlDown).row
    row = lastRow + 1
    num = Cells(lastRow, 3).value + 1
     
    Application.EnableEvents = False
     
    '검색결과 라인 서식 적용
    SetContentsLine Range("B" & row), "AC", 1
    
    '데이터 출력
    Cells(row, 2).value = orderId
    Cells(row, 3).value = num
    Cells(row, 4).value = order(28)
    Cells(row, 5).value = order(5)
    Cells(row, 6).value = order(3)
    Cells(row, 7).value = order(4)
    Cells(row, 8).value = order(6)
    Cells(row, 9).value = order(7)
    Cells(row, 10).value = order(8)
    Cells(row, 11).value = order(9)
    Cells(row, 12).value = order(10)
    Cells(row, 13).value = order(11)
    Cells(row, 14).value = order(12)
    Cells(row, 15).value = order(13)
    Cells(row, 16).value = order(14)
    Cells(row, 17).value = order(15)
    Cells(row, 18).value = order(16)
    Cells(row, 19).value = order(17)
    Cells(row, 20).value = order(18)
    Cells(row, 21).value = order(19)
    Cells(row, 22).value = order(20)
    Cells(row, 23).value = order(21)
    Cells(row, 24).value = order(22)
    Cells(row, 25).value = order(23)
    Cells(row, 26).value = order(24)
    Cells(row, 27).value = order(25)
    Cells(row, 28).value = order(31)
    Cells(row, 29).value = order(26)
    Cells(row, 30).value = order(27)
    Cells(row, 9).Select
    
safe_exit_appendShtOrder:
    Application.EnableEvents = True
    
End Sub




Sub SetChangeValue(cRow, cCol, Target)
    Dim value As Variant
    
    Select Case cCol
        Case 16     '중량
            value = Target.value
            If Range("G" & cRow).value <> "수주" Then
                Update_Record_Column shtOrder, Range("B" & cRow).value, "중량", value
            End If
        Case 18     '발주
            value = ConvertDateFormat(Target.value)
            Update_Record_Column shtOrder, Range("B" & cRow).value, "발주", value
        Case 19     '납기
            value = ConvertDateFormat(Target.value)
            If Range("G" & cRow).value = "수주" Then
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "납기", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "납기", value
        Case 20     '입고
            value = ConvertDateFormat(Target.value)
            Update_Record_Column shtOrder, Range("B" & cRow).value, "입고", value
        Case 21     '납품
            value = ConvertDateFormat(Target.value)
            Update_Record_Column shtEstimate, Range("D" & cRow).value, "납품", value
            Update_Record_Column shtOrder, Range("B" & cRow).value, "납품", value
        Case 22     '명세서
            value = ConvertDateFormat(Target.value)
            If Range("G" & cRow).value = "수주" Then
                '수주테이블에 업데이트
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "명세서", value
                '결제이력에도 업데이트
                UpdateLastPayment Range("D" & cRow).value, Range("E" & cRow).value, "명세서", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "명세서", value
        Case 23     '계산서
            value = ConvertDateFormat(Target.value)
            If Range("G" & cRow).value = "수주" Then
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "계산서", value
                UpdateLastPayment Range("D" & cRow).value, Range("E" & cRow).value, "계산서", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "계산서", value
        Case 24     '결제
            value = ConvertDateFormat(Target.value)
            If Range("G" & cRow).value = "수주" Then
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "결제", value
                UpdateLastPayment Range("D" & cRow).value, Range("E" & cRow).value, "결제", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "결제", value
        Case 25     '결제월
            value = ConvertDateFormat(Target.value)
            If Range("G" & cRow).value = "수주" Then
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "결제월", value
                UpdateLastPayment Range("D" & cRow).value, Range("E" & cRow).value, "결제월", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "결제월", value
        Case 26     '결제수단
            value = Target.value
            If Range("G" & cRow).value = "수주" Then
                Update_Record_Column shtEstimate, Range("D" & cRow).value, "결제수단", value
                UpdateLastPayment Range("D" & cRow).value, Range("E" & cRow).value, "결제수단", value
            End If
            Update_Record_Column shtOrder, Range("B" & cRow).value, "결제수단", value
    End Select
    
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    Dim cRow, cCol As Long
    Dim t As Range
    Dim lastRow As Long
    
    If Target.Cells.count = 1 Then
        cRow = Target.row
        cCol = Target.Column
    
        SetChangeValue cRow, cCol, Target
    Else
        If Range("AC5").value <> "등록일자" Then
            MsgBox "시트 구조를 바꿀 수 없습니다.", vbInformation, "작업 확인"
            UndoLastAction
            Exit Sub
        End If
        
'        If InStr(Target.Address, "$6") = 0 Then
'            MsgBox "여러 셀을 한번에 변경할 수 없습니다.", vbInformation, "작업 확인"
'            UndoLastAction
'            Exit Sub
'        End If
        
        lastRow = Range("B7").End(xlDown).row
        
        If Not Intersect(Target, Range("R6", "Z" & lastRow)) Is Nothing Then
            For Each t In Intersect(Target, Range("R6", "Z" & lastRow))
                cRow = t.row
                cCol = t.Column
            
                SetChangeValue cRow, cCol, t
            Next
        End If
        If Not Intersect(Target, Range("P6", "P" & lastRow)) Is Nothing Then
            For Each t In Intersect(Target, Range("P6", "P" & lastRow))
                cRow = t.row
                cCol = t.Column
            
                SetChangeValue cRow, cCol, t
            Next
        End If
    End If
        
End Sub