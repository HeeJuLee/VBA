Option Explicit

Dim isKeyworldSearch As Boolean
Dim searchResultCount As Long
Dim isEnd As Boolean
Dim isListInit As Boolean



'최초 로드 시
Private Sub Worksheet_Activate()
    Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    
    Me.txtFromDate.Visible = False
    Me.txtToDate.Visible = False
    Me.Image1.Visible = False
    Me.ImageList1.Visible = False
    Me.txtOperationID.Visible = False
    
    isKeyworldSearch = False
    isEnd = False
    
'    If isListInit = False Then
'        InitializeLswOperationList
'        OperationSearch
'    End If
End Sub

'운영비 데이터를 검색해서 시트에 출력
Sub OperationSearch()
    Dim db As Variant
    
    '운영비 데이터를 읽어옴
    db = Get_DB(shtOperation, False, False)
      
    If Not IsEmpty(db) Then
        If txtFromDate.value <> "" Then db = Filtered_DB(db, ">=" & txtFromDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    If Not IsEmpty(db) Then
        If txtToDate.value <> "" Then db = Filtered_DB(db, "<=" & txtToDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    
    If Not IsEmpty(db) Then
        If Me.txtKeyword.value <> "" Then db = Filtered_DB(db, Me.txtKeyword.value)     '모든 필드에서 키워드 검색
    End If
    
    '기존 검색결과 지우기
    ClearOperationSearchResult
    
    '검색결과를 화면에 출력
    If IsEmpty(db) Then
        searchResultCount = 0     '검색결과 건수는 0
    Else
        '검색결과 건수를 저장
        searchResultCount = UBound(db, 1)
        
        '검색결과 리스트뷰에 출력
        SetOperationList (db)
    End If
    
    '검색 결과 건수 출력
    Me.lblSearchResult.Caption = searchResultCount & "건"
    
    If isKeyworldSearch = True Then
        Me.txtKeyword.Activate
    End If
    
    isEnd = False
    
End Sub

Sub SetOperationList(db)
    Dim i As Long
    Dim li As ListItem
    Dim income, expense As Long
    
    With Me.ImageList1.ListImages
        .Add , , Me.Image1.Picture
    End With
    
     '리스트뷰 값 설정
    With Me.lswOperationList
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        .HideColumnHeaders = False
        .HideSelection = True
        .FullRowSelect = True
        .MultiSelect = True
        .LabelEdit = lvwManual
        .SmallIcons = Me.ImageList1
        .Sorted = False
        
        .ColumnHeaders.Clear

        .ColumnHeaders.Add , , "ID", 0
        .ColumnHeaders.Add , , "No", 40, lvwColumnCenter
        .ColumnHeaders.Add , , "등록일자", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "고정비", 60, lvwColumnCenter
        .ColumnHeaders.Add , , "수입지출", 70, lvwColumnCenter
        .ColumnHeaders.Add , , "분류", 55, lvwColumnCenter
        .ColumnHeaders.Add , , "거래처", 100
        .ColumnHeaders.Add , , "품목", 250
        .ColumnHeaders.Add , , "금액", 80, lvwColumnRight
        .ColumnHeaders.Add , , "명세서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "계산서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "결제", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "부가세", 80, lvwColumnRight
    
        income = 0
        expense = 0
        
        For i = 1 To UBound(db)
           
            Set li = .ListItems.Add(, , db(i, 1))
            li.ListSubItems.Add , , i
            li.ListSubItems.Add , , db(i, 14)
            li.ListSubItems.Add , , db(i, 15)
            li.ListSubItems.Add , , db(i, 3)
            li.ListSubItems.Add , , db(i, 5)
            li.ListSubItems.Add , , db(i, 7)
            li.ListSubItems.Add , , db(i, 8)
            li.ListSubItems.Add , , Format(db(i, 9), "#,##0")
            li.ListSubItems.Add , , db(i, 10)
            li.ListSubItems.Add , , db(i, 11)
            li.ListSubItems.Add , , db(i, 12)
            li.ListSubItems.Add , , Format(db(i, 13), "#,##0")
            
            If db(i, 3) = "수입" Then
                income = income + db(i, 9)
            ElseIf db(i, 3) = "지출" Then
                expense = expense + db(i, 9)
            End If
        Next
    End With
    
    shtOperationAdmin.Range("Q6").value = income
    shtOperationAdmin.Range("T6").value = expense
    
End Sub

'발주 검색결과를 화면에서 지움
Sub ClearOperationSearchResult()
    
    '리스트뷰 내용 삭제
    Me.lswOperationList.ListItems.Clear
    
    '입력 셀 내용 지움
    'ClearOperationInput
    
End Sub

Sub InitializeLswOperationList()
    Dim db As Variant
    Dim i, j, totalCost As Long
    Dim li As ListItem
    Dim fullPath As String
    
    With Me.ImageList1.ListImages
        .Add , , Me.Image1.Picture
    End With
    
     '리스트뷰 값 설정
    With Me.lswOperationList
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        .HideColumnHeaders = False
        .HideSelection = True
        .FullRowSelect = True
        .MultiSelect = True
        .LabelEdit = lvwManual
        .SmallIcons = Me.ImageList1
        
        .ColumnHeaders.Clear

        .ColumnHeaders.Add , , "ID", 0
        .ColumnHeaders.Add , , "No", 40, lvwColumnCenter
        .ColumnHeaders.Add , , "등록일자", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "고정비", 60, lvwColumnCenter
        .ColumnHeaders.Add , , "수입지출", 70, lvwColumnCenter
        .ColumnHeaders.Add , , "분류", 55, lvwColumnCenter
        .ColumnHeaders.Add , , "거래처", 100
        .ColumnHeaders.Add , , "품목", 250
        .ColumnHeaders.Add , , "금액", 80, lvwColumnRight
        .ColumnHeaders.Add , , "명세서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "계산서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "결제", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "부가세", 80, lvwColumnRight
    End With
    
    isListInit = True
End Sub

Sub SelectItemLswOperation(selectedID As Variant)
    Dim i As Long
    
    With Me.lswOperationList
        If Not IsMissing(selectedID) Then
            For i = 1 To .ListItems.count
                If selectedID = .ListItems(i).Text Then
                    .selectedItem = .ListItems(i)
                    .selectedItem.EnsureVisible
                Else
                    .ListItems(i).Selected = False
                End If
            Next
        End If
    End With
End Sub

Sub ClearOperationInput()
    Range("D6:N6") = ""
    Range("D6").value = Date
End Sub

'운영비 등록
Sub InsertOperation()
    Dim YN As VbMsgBoxResult
    Dim count As Long
    Dim li As ListItem

    count = 0
    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then count = count + 1
    Next
    If count <= 1 Then
        If Range("I6").value = "" Then MsgBox "품목을 입력하세요.": Exit Sub
        If Range("D6") = "" Then MsgBox "날짜를 입력하세요.": Exit Sub
        
        '운영비에 저장
        Insert_Record shtOperation, , Range("F6").value, , Range("G6"), , Range("H6"), Range("I6"), Range("J6"), Range("K6"), Range("L6"), Range("M6"), Range("N6"), Range("D6"), Range("E6")
    Else
        '여러개 선택 후 등록 시 대량 등록 진행
        YN = MsgBox("선택한 " & count & "개 항목을 등록합니다.", vbYesNo)
        If YN = vbNo Then Exit Sub
        
        For Each li In Me.lswOperationList.ListItems
            If li.Selected = True Then
                Insert_Record shtOperation, , li.SubItems(4), , li.SubItems(5), , li.SubItems(6), li.SubItems(7), li.SubItems(8), li.SubItems(9), li.SubItems(10), li.SubItems(11), li.SubItems(12), Date, li.SubItems(3)
            End If
        Next
    End If
    
    OperationSearch
    
    '등록한 아이템 선택
    Me.txtOperationID.value = Get_LastID(shtOperation)
    SelectItemLswOperation Me.txtOperationID.value

End Sub

'운영비 수정
Sub UpdateOperation()
    Dim cost As Variant

    If Me.txtOperationID.value = "" Then MsgBox "수정할 항목을 선택하세요.": Exit Sub
    
    If Range("I6").value = "" Then MsgBox "품목을 입력하세요.": Exit Sub
    If Range("D6") = "" Then MsgBox "날짜를 입력하세요.": Exit Sub
    
    '기존 운영비 업데이트
    Update_Record shtOperation, Me.txtOperationID.value, , Range("F6").value, , Range("G6"), , Range("H6"), Range("I6"), Range("J6"), Range("K6"), Range("L6"), Range("M6"), Range("N6"), Range("D6"), Range("E6")
    
    OperationSearch
    
    '등록한 아이템 선택
    SelectItemLswOperation Me.txtOperationID.value
End Sub

'운영비 삭제
Sub DeleteOperation()
    Dim YN As VbMsgBoxResult
    Dim count As Long
    Dim li As ListItem

    count = 0
    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then count = count + 1
    Next
    If count = 0 Then MsgBox "삭제할 항목을 선택하세요.": Exit Sub
    
    YN = MsgBox("선택한 " & count & "개 항목을 삭제합니다.", vbYesNo)
    If YN = vbNo Then Exit Sub

    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then
            Delete_Record shtOperation, li.Text
        End If
    Next
    
    Me.txtOperationID.value = ""
    ClearOperationInput
    OperationSearch
End Sub

Sub EmptyProcedure()

End Sub


Sub GoToEnd()
    With Me.lswOperationList
        If .ListItems.count > 0 Then
            If isEnd = False Then
                .selectedItem = .ListItems(.ListItems.count)
                .selectedItem.EnsureVisible
                isEnd = True
            Else
                .selectedItem = .ListItems(1)
                .selectedItem.EnsureVisible
                isEnd = False
            End If
        End If
    End With
    
    lswOperationList_Click
End Sub

'전체 선택 시 '월'만 전체로 바꿈
Private Sub optAll_Click()
    If Me.cboSelectMonth.value = "전체" Then
        OperationSearch
    Else
        Me.cboSelectMonth.value = "전체"
    End If
    
End Sub

'조회기간 - 이번달
Private Sub optThisMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = Month(Date)
    
    txtFromDate.value = DateSerial(Y, M, 1)
    txtToDate.value = DateSerial(Y, M + 1, 0)
    
    Me.cboSelectYear.value = Y & "년"
    Me.cboSelectMonth.value = M & "월"      '실제 검색은 cboSelectMonth_change()에서 검색 됨
    
End Sub

'조회기간 - 이전달
Private Sub optLastMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = Month(Date)
    
    txtFromDate.value = DateSerial(Y, M - 1, 1)
    txtToDate.value = DateSerial(Y, M, 0)
    
    If M = 1 Then
        Me.cboSelectYear.value = Y - 1 & "년"
        Me.cboSelectMonth.value = "12월"
    Else
        Me.cboSelectYear.value = Y & "년"
        Me.cboSelectMonth.value = M - 1 & "월"
    End If
    
End Sub


Private Sub lswOperationList_Click()
    With Me.lswOperationList
        If Not .selectedItem Is Nothing Then
            Me.txtOperationID.value = .selectedItem.Text
            Range("D6") = Format(.selectedItem.ListSubItems(2), "mm/dd")
            Range("E6") = .selectedItem.ListSubItems(3)
            Range("F6") = .selectedItem.ListSubItems(4)
            Range("G6") = .selectedItem.ListSubItems(5)
            Range("H6") = .selectedItem.ListSubItems(6)
            Range("I6") = .selectedItem.ListSubItems(7)
            If IsNumeric(.selectedItem.ListSubItems(8)) Then
                Range("J6") = CLng(.selectedItem.ListSubItems(8))
            Else
                Range("J6") = .selectedItem.ListSubItems(8)
            End If
            Range("K6") = Format(.selectedItem.ListSubItems(9), "mm/dd")
            Range("L6") = Format(.selectedItem.ListSubItems(10), "mm/dd")
            Range("M6") = Format(.selectedItem.ListSubItems(11), "mm/dd")
            If IsNumeric(.selectedItem.ListSubItems(12)) Then
                Range("N6") = CLng(.selectedItem.ListSubItems(12))
            Else
                Range("N6") = .selectedItem.ListSubItems(12)
            End If
        End If
    End With
End Sub


Private Sub lswOperationList_ColumnClick(ByVal ColumnHeader As MSComctlLib.ColumnHeader)
    
    With Me.lswOperationList
        .SortKey = ColumnHeader.Index - 1
        If .SortOrder = lvwAscending Then
            .SortOrder = lvwDescending
        Else
            .SortOrder = lvwAscending
        End If
        .Sorted = True
    End With
    
End Sub

Private Sub cboSelectMonth_DropButtonClick()
    If Me.cboSelectMonth.LineCount = 0 Then
        Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    End If
End Sub

Private Sub cboSelectYear_DropButtonClick()
    If Me.cboSelectYear.ListCount = 0 Then
        Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    End If
End Sub


'키워드 검색 입력 시
Private Sub txtKeyword_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch = True
        OperationSearch
        isKeyworldSearch = False
    End If
End Sub


'년 선택
Private Sub cboSelectYear_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = Month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
        If Me.cboSelectMonth.value = "전체" Then
            '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            '월 선택이 변경되면서 cboSelectMonth_Change()에서 검색 실행
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        '년도를 선택한 경우
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
        End If
        
        OperationSearch
    End If
    
End Sub

'월 선택
Private Sub cboSelectMonth_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = Month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
         If Me.cboSelectMonth.value = "전체" Then
             '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
            
            If Me.optAll.value = False Then Me.optAll.value = True Else OperationSearch
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
            
            OperationSearch
        End If
    End If
End Sub


Private Sub Worksheet_Change(ByVal Target As Range)
    If Target.Address = "$J$6" Then
        If Target.value = "" Then
            Range("N6").value = ""
        ElseIf IsNumeric(Target.value) Then
            Range("N6").value = Target.value * 0.1
        End If
    End If
End Sub

Sub DeleteKeyword1()
    Me.txtKeyword.value = ""
    OrderSearch
End Sub

Sub DeleteKeyword2()
    Me.txtKeyword2.value = ""
    OrderSearch
End Sub