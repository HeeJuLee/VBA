Option Explicit

Dim isKeyworldSearch As Boolean
Dim searchResultCount As Long
Dim isEnd As Boolean
Dim isListInit As Boolean
Dim mouseX As Integer

Private Sub btnClear_Click()
    ClearOperationInput
End Sub

Private Sub btnInsert_Click()
    InsertOperation
End Sub

Private Sub btnUpdate_Click()
    UpdateOperation
End Sub

Private Sub btnDelete_Click()
    DeleteOperation
End Sub

Private Sub btnCopyFixedCost_Click()
    CopyFixedCost
End Sub

Private Sub lswOperationList_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As stdole.OLE_XPOS_PIXELS, ByVal Y As stdole.OLE_YPOS_PIXELS)
    mouseX = pointsPerPixelX * x
End Sub

'최초 로드 시
Private Sub Worksheet_Activate()
    Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    
    Me.txtFromDate.Visible = False
    Me.txtToDate.Visible = False
    Me.Image1.Visible = False
    Me.ImageList1.Visible = False
    Me.txtOperationID.Visible = False
    
    isKeyworldSearch = False
    isEnd = False
    
    If isListInit = False Then
        OperationSearch
    End If
End Sub

'운영비 데이터를 검색해서 시트에 출력
Sub OperationSearch()
    Dim db As Variant
    
    '운영비 데이터를 읽어옴
    db = Get_DB(shtOperation, False, False)
      
    If Not isEmpty(db) Then
        If txtFromDate.value <> "" Then db = Filtered_DB(db, ">=" & txtFromDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If txtToDate.value <> "" Then db = Filtered_DB(db, "<=" & txtToDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    
    If Not isEmpty(db) Then
        If Me.txtKeyword.value <> "" Then db = Filtered_DB(db, Me.txtKeyword.value)     '모든 필드에서 키워드 검색
    End If
    
    '기존 검색결과 지우기
    ClearOperationSearchResult
    
    '검색결과를 화면에 출력
    If isEmpty(db) Then
        searchResultCount = 0     '검색결과 건수는 0
    Else
        '검색결과 건수를 저장
        searchResultCount = UBound(db, 1)
        
        '검색결과 리스트뷰에 출력
        SetOperationList (db)
    End If
    
    '검색 결과 건수 출력
    Me.lblSearchResult.Caption = searchResultCount & "건"
    
    If isKeyworldSearch = True Then
        Me.txtKeyword.Activate
    End If
    
    isEnd = False
    isListInit = True
End Sub

Sub SetOperationList(db)
    Dim i As Long
    Dim li As ListItem
    Dim income, expense As Long
    
    With Me.ImageList1.ListImages
        .Add , , Me.Image1.Picture
    End With
    
     '리스트뷰 값 설정
    With Me.lswOperationList
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        .HideColumnHeaders = False
        .HideSelection = True
        .FullRowSelect = True
        .MultiSelect = True
        .LabelEdit = lvwManual
        .SmallIcons = Me.ImageList1
        .Sorted = False
        
        .ColumnHeaders.Clear

        .ColumnHeaders.Add , , "ID", 0
        .ColumnHeaders.Add , , "No", 40, lvwColumnCenter
        .ColumnHeaders.Add , , "등록일자", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "고정비", 60, lvwColumnCenter
        .ColumnHeaders.Add , , "수입지출", 70, lvwColumnCenter
        .ColumnHeaders.Add , , "분류", 55, lvwColumnCenter
        .ColumnHeaders.Add , , "거래처", 100
        .ColumnHeaders.Add , , "품목", 250
        .ColumnHeaders.Add , , "금액", 80, lvwColumnRight
        .ColumnHeaders.Add , , "명세서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "계산서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "결제", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "결제수단", 66, lvwColumnCenter
        .ColumnHeaders.Add , , "부가세", 80, lvwColumnRight
    
        income = 0
        expense = 0
        
        For i = 1 To UBound(db)
           
            Set li = .ListItems.Add(, , db(i, 1))
            li.ListSubItems.Add , , i
            li.ListSubItems.Add , , db(i, 14)
            li.ListSubItems.Add , , db(i, 15)
            li.ListSubItems.Add , , db(i, 3)
            li.ListSubItems.Add , , db(i, 5)
            li.ListSubItems.Add , , db(i, 7)
            li.ListSubItems.Add , , db(i, 8)
            li.ListSubItems.Add , , Format(db(i, 9), "#,##0")
            li.ListSubItems.Add , , db(i, 10)
            li.ListSubItems.Add , , db(i, 11)
            li.ListSubItems.Add , , db(i, 12)
            li.ListSubItems.Add , , db(i, 4)
            li.ListSubItems.Add , , Format(db(i, 13), "#,##0")
            
            If db(i, 3) = "수입" Then
                income = income + db(i, 9)
            ElseIf db(i, 3) = "지출" Then
                expense = expense + db(i, 9)
            End If
        Next
        
    End With
    
    shtOperationAdmin.Range("R6").value = income
    shtOperationAdmin.Range("U6").value = expense
    
End Sub

'발주 검색결과를 화면에서 지움
Sub ClearOperationSearchResult()
    
    '리스트뷰 내용 삭제
    Me.lswOperationList.ListItems.Clear
    
    '입력 셀 내용 지움
    'ClearOperationInput
    
End Sub


Sub SelectItemLswOperation(selectedID As Variant)
    Dim i As Long
    
    With Me.lswOperationList
        If Not IsMissing(selectedID) Then
            For i = 1 To .ListItems.count
                If selectedID = .ListItems(i).Text Then
                    .selectedItem = .ListItems(i)
                    .selectedItem.EnsureVisible
                Else
                    .ListItems(i).Selected = False
                End If
            Next
            .Activate
        End If
    End With
End Sub

Sub ClearOperationInput()
    Range("D6:O6") = ""
    Range("D6").value = Date
End Sub

'운영비 등록
Sub InsertOperation()
    Dim yn As VbMsgBoxResult
    Dim count As Long
    Dim li As ListItem

    count = 0
    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then count = count + 1
    Next
    If count <= 1 Then
        If Range("I20").value = "" Then MsgBox "품목을 입력하세요.": Exit Sub
        If Range("D20") = "" Then MsgBox "날짜를 입력하세요.": Exit Sub
        
        '운영비에 저장
        Insert_Record shtOperation, , Range("F20").value, Range("N20"), Range("G20"), , Range("H20"), Range("I20"), Range("J20"), Range("K20"), Range("L20"), Range("M20"), Range("O20"), Range("D20"), Range("E20")
    Else
        '여러개 선택 후 등록 시 대량 등록 진행
        yn = MsgBox("선택한 " & count & "개 항목을 등록합니다.", vbYesNo)
        If yn = vbNo Then Exit Sub
        
        For Each li In Me.lswOperationList.ListItems
            If li.Selected = True Then
                Insert_Record shtOperation, , li.SubItems(4), li.SubItems(12), li.SubItems(5), , li.SubItems(6), li.SubItems(7), li.SubItems(8), li.SubItems(9), li.SubItems(10), li.SubItems(11), li.SubItems(13), Date, li.SubItems(3)
            End If
        Next
    End If
    
    OperationSearch
    
    '등록한 아이템 선택
    Me.txtOperationID.value = Get_LastID(shtOperation)
    SelectItemLswOperation Me.txtOperationID.value

End Sub

'고정비 복사
Sub CopyFixedCost()
    Dim db As Variant
    Dim Y, M As Variant
    Dim fromDate, toDate As Date
    Dim i, count As Long
    Dim yn As VbMsgBoxResult

    M = month(Date)
    If M = 1 Then
        Y = Year(Date) - 1
        M = 12
    Else
        Y = Year(Date)
        M = M - 1
    End If
    
    fromDate = DateSerial(Y, M, 1)
    toDate = DateSerial(Year(Date), month(Date), 1)

    db = Get_DB(shtOperation)
    If Not isEmpty(db) Then
        db = Filtered_DB(db, "고정비", 15)
    End If
    If Not isEmpty(db) Then
        db = Filtered_DB(db, ">=" & fromDate, 14)
    End If
    If Not isEmpty(db) Then
        db = Filtered_DB(db, "<" & toDate, 14)
    End If
    
    count = UBound(db)
    If count = 0 Then
        MsgBox Y & "년 " & M & "월에 고정비 항목이 없습니다.", vbInformation, "작업확인"
    Else
        yn = MsgBox(Y & "년 " & M & "월에서 " & count & "개의 고정비 항목을 복사합니다.", vbYesNo, "작업확인")
        If yn = vbNo Then Exit Sub
        
        For i = 1 To count
            Insert_Record shtOperation, , db(i, 3), db(i, 4), db(i, 5), db(i, 6), db(i, 7), db(i, 8), db(i, 9), db(i, 10), db(i, 11), db(i, 12), db(i, 13), toDate, db(i, 15)
        Next
    End If
    
    OperationSearch

    isEnd = False
    GoToEnd
End Sub

'운영비 수정
Sub UpdateOperation()
    Dim cost As Variant

    If Me.txtOperationID.value = "" Then MsgBox "수정할 항목을 선택하세요.": Exit Sub
    
    If Range("I20").value = "" Then MsgBox "품목을 입력하세요.": Exit Sub
    If Range("D20") = "" Then MsgBox "날짜를 입력하세요.": Exit Sub
    
    '기존 운영비 업데이트
    Update_Record shtOperation, Me.txtOperationID.value, Range("N20"), Range("F20").value, , Range("G20"), , Range("H20"), Range("I20"), Range("J20"), Range("K20"), Range("L20"), Range("M20"), Range("O20"), Range("D20"), Range("E20")
    
    OperationSearch
    
    '등록한 아이템 선택
    SelectItemLswOperation Me.txtOperationID.value
End Sub

'운영비 삭제
Sub DeleteOperation()
    Dim yn As VbMsgBoxResult
    Dim count As Long
    Dim li As ListItem

    count = 0
    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then count = count + 1
    Next
    If count = 0 Then MsgBox "삭제할 항목을 선택하세요.": Exit Sub
    
    yn = MsgBox("선택한 " & count & "개 항목을 삭제합니다.", vbYesNo)
    If yn = vbNo Then Exit Sub

    For Each li In Me.lswOperationList.ListItems
        If li.Selected = True Then
            Delete_Record shtOperation, li.Text
        End If
    Next
    
    Me.txtOperationID.value = ""
    ClearOperationInput
    OperationSearch
End Sub

Sub EmptyProcedure()

End Sub


Sub GoToEnd()
    With Me.lswOperationList
        If .ListItems.count > 0 Then
            .selectedItem.Selected = False
            If isEnd = False Then
                .selectedItem = .ListItems(.ListItems.count)
                .selectedItem.EnsureVisible
                isEnd = True
                ClearOperationInput
            Else
                .selectedItem = .ListItems(1)
                .selectedItem.EnsureVisible
                isEnd = False
            End If
        End If
    End With
    
End Sub

'전체 선택 시 '월'만 전체로 바꿈
Private Sub optAll_Click()
    If Me.cboSelectMonth.value = "전체" Then
        OperationSearch
    Else
        Me.cboSelectMonth.value = "전체"
    End If
    
End Sub

'조회기간 - 이번달
Private Sub optThisMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    txtFromDate.value = DateSerial(Y, M, 1)
    txtToDate.value = DateSerial(Y, M + 1, 0)
    
    Me.cboSelectYear.value = Y & "년"
    Me.cboSelectMonth.value = M & "월"      '실제 검색은 cboSelectMonth_change()에서 검색 됨
    
End Sub

'조회기간 - 이전달
Private Sub optLastMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    txtFromDate.value = DateSerial(Y, M - 1, 1)
    txtToDate.value = DateSerial(Y, M, 0)
    
    If M = 1 Then
        Me.cboSelectYear.value = Y - 1 & "년"
        Me.cboSelectMonth.value = "12월"
    Else
        Me.cboSelectYear.value = Y & "년"
        Me.cboSelectMonth.value = M - 1 & "월"
    End If
    
End Sub


Private Sub lswOperationList_Click()
    With Me.lswOperationList
        If Not .selectedItem Is Nothing Then
            Me.txtOperationID.value = .selectedItem.Text
            Range("D20") = Format(.selectedItem.ListSubItems(2), "mm/dd")
            Range("E20") = .selectedItem.ListSubItems(3)
            Range("F20") = .selectedItem.ListSubItems(4)
            Range("G20") = .selectedItem.ListSubItems(5)
            Range("H20") = .selectedItem.ListSubItems(6)
            Range("I20") = .selectedItem.ListSubItems(7)
            If IsNumeric(.selectedItem.ListSubItems(8)) Then
                Range("J20") = CLng(.selectedItem.ListSubItems(8))
            Else
                Range("J20") = .selectedItem.ListSubItems(8)
            End If
            Range("K20") = Format(.selectedItem.ListSubItems(9), "mm/dd")
            Range("L20") = Format(.selectedItem.ListSubItems(10), "mm/dd")
            Range("M20") = Format(.selectedItem.ListSubItems(11), "mm/dd")
            Range("N20") = .selectedItem.ListSubItems(12)
            If IsNumeric(.selectedItem.ListSubItems(13)) Then
                Range("O20") = CLng(.selectedItem.ListSubItems(13))
            Else
                Range("O20") = .selectedItem.ListSubItems(13)
            End If
        End If
    End With
End Sub


Private Sub lswOperationList_ColumnClick(ByVal ColumnHeader As MSComctlLib.ColumnHeader)
    
    With Me.lswOperationList
        .SortKey = ColumnHeader.Index - 1
        If .SortOrder = lvwAscending Then
            .SortOrder = lvwDescending
        Else
            .SortOrder = lvwAscending
        End If
        .Sorted = True
    End With
    
End Sub

Private Sub cboSelectMonth_DropButtonClick()
    If Me.cboSelectMonth.LineCount = 0 Then
        Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    End If
End Sub

Private Sub cboSelectYear_DropButtonClick()
    If Me.cboSelectYear.ListCount = 0 Then
        Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    End If
End Sub


'키워드 검색 입력 시
Private Sub txtKeyword_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch = True
        OperationSearch
        isKeyworldSearch = False
    End If
End Sub


'년 선택
Private Sub cboSelectYear_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
        If Me.cboSelectMonth.value = "전체" Then
            '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            '월 선택이 변경되면서 cboSelectMonth_Change()에서 검색 실행
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        '년도를 선택한 경우
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
        End If
        
        OperationSearch
    End If
    
End Sub

'월 선택
Private Sub cboSelectMonth_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
         If Me.cboSelectMonth.value = "전체" Then
             '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
            
            If Me.optAll.value = False Then Me.optAll.value = True Else OperationSearch
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
            
            OperationSearch
        End If
    End If
End Sub


Private Sub Worksheet_Change(ByVal target As Range)
    If target.Address = "$J$6" Then
        If target.value = "" Then
            Range("N6").value = ""
        ElseIf IsNumeric(target.value) Then
            Range("N6").value = target.value * 0.1
        End If
    End If
End Sub

Sub DeleteKeyword1()
    Me.txtKeyword.value = ""
    OrderSearch
End Sub

Sub DeleteKeyword2()
    Me.txtKeyword2.value = ""
    OrderSearch
End Sub