Option Explicit

Dim isKeyworldSearch As Boolean
Dim searchResultCount As Long
Dim isEnd As Boolean
Dim isListInit As Boolean
Dim mouseX As Integer

Private Sub btnInsert_Click()
    InsertOperation
End Sub

Private Sub btnUpdate_Click()
    UpdateOperation
End Sub

Private Sub btnDelete_Click()
    DeleteOperation
End Sub

Private Sub btnCopyFixedCost_Click()
    CopyFixedCost
End Sub

Private Sub lswOperationList_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As stdole.OLE_XPOS_PIXELS, ByVal Y As stdole.OLE_YPOS_PIXELS)
    mouseX = pointsPerPixelX * x
End Sub

'최초 로드 시
Private Sub Worksheet_Activate()
    Dim i As Long
    Dim Y As Long
    
    Y = Year(Date)
    
    Me.cboSelectYear.List = Array("전체")
    Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    
    For i = 0 To Y - 2005
        Me.cboSelectYear.AddItem Y - i & "년"
    Next
    
    Me.txtFromDate.Visible = False
    Me.txtToDate.Visible = False
    Me.txtOperationID.Visible = False
    
    isKeyworldSearch = False
    isEnd = False
    
End Sub

'운영비 데이터를 검색해서 시트에 출력
Sub OperationSearch()
    Dim db As Variant
    
    '운영비 데이터를 읽어옴
    db = Get_DB(shtOperation, False, False)
      
    If Not isEmpty(db) Then
        If txtFromDate.value <> "" Then db = Filtered_DB(db, ">=" & txtFromDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    If Not isEmpty(db) Then
        If txtToDate.value <> "" Then db = Filtered_DB(db, "<=" & txtToDate.value, 14)    '등록일자 필드와 날짜 비교
    End If
    
    If Not isEmpty(db) Then
        If Me.txtKeyword.value <> "" Then db = Filtered_DB(db, Me.txtKeyword.value)     '모든 필드에서 키워드 검색
    End If
    
    '기존 검색결과 지우기
    ClearOperationSearchResult
    
    '검색결과를 화면에 출력
    If isEmpty(db) Then
        searchResultCount = 0     '검색결과 건수는 0
    Else
        '검색결과 건수를 저장
        searchResultCount = UBound(db, 1)
        
        '검색결과 라인 서식 적용
        SetContentsLine shtOperationAdmin.Range("B7"), "P", searchResultCount
        
        '순번 출력
        SequenceToRng shtOperationAdmin.Range("C7"), UBound(db, 1)
        
        '검색결과 출력
        ArrayToRng shtOperationAdmin.Range("B7"), db, "1, ,14,15,3,5,7,8,9,10,11,12,4,13,16"
        
        '수입 지출 출력
        SetIncome db
    End If
    
    '검색 결과 건수 출력
    Me.lblSearchResult.Caption = searchResultCount & "건"
    
    If isKeyworldSearch = True Then
        Me.txtKeyword.Activate
    End If
    
    isEnd = False
    GoToEnd
    
    Application.EnableEvents = True
End Sub

Sub SetIncome(db)
    Dim i As Long
    Dim income, expense As Double
    
    income = 0
    expense = 0
    
    For i = 1 To UBound(db)
        If Not IsNull(db(i, 9)) And db(i, 9) <> "" Then
            If db(i, 3) = "수입" Then
                income = income + db(i, 9)
            ElseIf db(i, 3) = "지출" Then
                expense = expense + db(i, 9)
            End If
        End If
    Next
    
    Range("S6").value = income
    Range("V6").value = expense
    
End Sub

'발주 검색결과를 화면에서 지움
Sub ClearOperationSearchResult()
    
    '아래쪽 라인 서식 지움
    ClearContentsLine shtOperationAdmin.Range("B7"), "P"
    
    '6번 순번 열을 기준으로 지움
    ClearContentsBelow shtOperationAdmin.Range("B7"), "P"
    
End Sub

'운영비 추가
Sub InsertOperation()
    Dim id, lastRow, row, num As Long
    
    '운영비에 빈 레코드 하나 추가
    Insert_Record shtOperation, , , , , , , , , , , , 0, Date
    
    '시트 맨 아래에 라인 추가
    id = Get_LastID(shtOperation)
    lastRow = Range("B7").End(xlDown).row
    row = lastRow + 1
    num = Cells(lastRow, 3).value + 1
     
    '검색결과 라인 서식 적용
    SetContentsLine Range("B" & row), "P", 1
    
    Cells(row, 2).value = id
    Cells(row, 3).value = num
    Cells(row, 4).value = Date
    Application.EnableEvents = False
    Cells(row, 15).value = 0
    Application.EnableEvents = True
    
    Cells(row, 6).Select
End Sub

'고정비 복사
Sub CopyFixedCost()
    Dim db As Variant
    Dim Y, M As Variant
    Dim fromDate, toDate As Date
    Dim i, count As Long
    Dim yn As VbMsgBoxResult

    M = month(Date)
    If M = 1 Then
        Y = Year(Date) - 1
        M = 12
    Else
        Y = Year(Date)
        M = M - 1
    End If
    
    fromDate = DateSerial(Y, M, 1)
    toDate = DateSerial(Year(Date), month(Date), 1)

    db = Get_DB(shtOperation)
    If Not isEmpty(db) Then
        db = Filtered_DB(db, "고정비", 15)
    End If
    If Not isEmpty(db) Then
        db = Filtered_DB(db, ">=" & fromDate, 14)
    End If
    If Not isEmpty(db) Then
        db = Filtered_DB(db, "<" & toDate, 14)
    End If
    
    If isEmpty(db) Then
        MsgBox Y & "년 " & M & "월에 고정비 항목이 없습니다.", vbInformation, "작업확인"
    Else
        count = UBound(db)
        yn = MsgBox(Y & "년 " & M & "월에서 " & count & "개의 고정비 항목을 복사합니다.", vbYesNo, "작업확인")
        If yn = vbNo Then Exit Sub
        
        Application.ScreenUpdating = False
        Application.Calculation = xlCalculationManual
        For i = 1 To count
            Insert_Record shtOperation, , db(i, 3), db(i, 4), db(i, 5), db(i, 6), db(i, 7), db(i, 8), db(i, 9), db(i, 10), db(i, 11), db(i, 12), db(i, 13), toDate, db(i, 15), db(i, 16)
        Next
        Application.ScreenUpdating = True
        Application.Calculation = xlCalculationAutomatic
        
        OperationSearch
    End If
    
End Sub

'운영비 수정
Sub UpdateOperation()
    Dim cost As Variant

    If Me.txtOperationID.value = "" Then MsgBox "수정할 항목을 선택하세요.": Exit Sub
    
    If Range("I20").value = "" Then MsgBox "품목을 입력하세요.": Exit Sub
    If Range("D20") = "" Then MsgBox "날짜를 입력하세요.": Exit Sub
    
    '기존 운영비 업데이트
    Update_Record shtOperation, Me.txtOperationID.value, Range("N20"), Range("F20").value, , Range("G20"), , Range("H20"), Range("I20"), Range("J20"), Range("K20"), Range("L20"), Range("M20"), Range("O20"), Range("D20"), Range("E20")
    
    OperationSearch
    
    '등록한 아이템 선택
    SelectItemLswOperation Me.txtOperationID.value
End Sub

'운영비 삭제
Sub DeleteOperation()
    Dim cRow As Long
    Dim yn As VbMsgBoxResult
    
    '선택한 행 번호
    cRow = Selection.row

    '데이터가 있는 행이 아닐 경우는 중지
    If cRow < 7 Or Range("B" & cRow).value = "" Then
        MsgBox "삭제할 행을 선택하신 후 '삭제' 버튼을 클릭하세요."
        End
    End If

    '안내 문구 출력
    yn = MsgBox("선택한 운영비 항목을 정말로 삭제하시겠습니까?" & vbCrLf & "삭제한 정보는 복구가 불가능합니다.", vbYesNo)
    If yn = vbNo Then Exit Sub
    
    'DB에서 운영비 정보 삭제
    Delete_Record shtOperation, Range("B" & cRow).value

    OperationSearch
End Sub

Sub EmptyProcedure()

End Sub

Sub GoToEnd()
    Dim endRow As Long
    
    With shtOperationAdmin
        .Activate
        If isEnd = False Then
            .Range("H" & .Range("C7").End(xlDown).row).Select
            isEnd = True
        Else
            .Range("H7").Select
            isEnd = False
        End If
    End With
End Sub


'전체 선택 시 '월'만 전체로 바꿈
Private Sub optAll_Click()
    If Me.cboSelectMonth.value = "전체" Then
        OperationSearch
    Else
        Me.cboSelectMonth.value = "전체"
    End If
    
End Sub

'조회기간 - 이번달
Private Sub optThisMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    txtFromDate.value = DateSerial(Y, M, 1)
    txtToDate.value = DateSerial(Y, M + 1, 0)
    
    Me.cboSelectYear.value = Y & "년"
    Me.cboSelectMonth.value = M & "월"      '실제 검색은 cboSelectMonth_change()에서 검색 됨
    
End Sub

'조회기간 - 이전달
Private Sub optLastMonth_Click()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    
    txtFromDate.value = DateSerial(Y, M - 1, 1)
    txtToDate.value = DateSerial(Y, M, 0)
    
    If M = 1 Then
        Me.cboSelectYear.value = Y - 1 & "년"
        Me.cboSelectMonth.value = "12월"
    Else
        Me.cboSelectYear.value = Y & "년"
        Me.cboSelectMonth.value = M - 1 & "월"
    End If
    
End Sub


Private Sub lswOperationList_Click()
    With Me.lswOperationList
        If Not .selectedItem Is Nothing Then
            Me.txtOperationID.value = .selectedItem.Text
            Range("D20") = Format(.selectedItem.ListSubItems(2), "mm/dd")
            Range("E20") = .selectedItem.ListSubItems(3)
            Range("F20") = .selectedItem.ListSubItems(4)
            Range("G20") = .selectedItem.ListSubItems(5)
            Range("H20") = .selectedItem.ListSubItems(6)
            Range("I20") = .selectedItem.ListSubItems(7)
            If IsNumeric(.selectedItem.ListSubItems(8)) Then
                Range("J20") = CLng(.selectedItem.ListSubItems(8))
            Else
                Range("J20") = .selectedItem.ListSubItems(8)
            End If
            Range("K20") = Format(.selectedItem.ListSubItems(9), "mm/dd")
            Range("L20") = Format(.selectedItem.ListSubItems(10), "mm/dd")
            Range("M20") = Format(.selectedItem.ListSubItems(11), "mm/dd")
            Range("N20") = .selectedItem.ListSubItems(12)
            If IsNumeric(.selectedItem.ListSubItems(13)) Then
                Range("O20") = CLng(.selectedItem.ListSubItems(13))
            Else
                Range("O20") = .selectedItem.ListSubItems(13)
            End If
        End If
    End With
End Sub


Private Sub lswOperationList_ColumnClick(ByVal ColumnHeader As MSComctlLib.ColumnHeader)
    
    With Me.lswOperationList
        .SortKey = ColumnHeader.Index - 1
        If .SortOrder = lvwAscending Then
            .SortOrder = lvwDescending
        Else
            .SortOrder = lvwAscending
        End If
        .Sorted = True
    End With
    
End Sub

Private Sub cboSelectMonth_DropButtonClick()
    If Me.cboSelectMonth.LineCount = 0 Then
        Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    End If
End Sub

Private Sub cboSelectYear_DropButtonClick()
    Dim i As Long
    Dim Y As Long
    
    If Me.cboSelectYear.ListCount = 0 Then
        Me.cboSelectYear.List = Array("전체")
        
        Y = Year(Date)
        For i = 0 To Y - 2005
            Me.cboSelectYear.AddItem Y - i & "년"
        Next
    End If
End Sub


'키워드 검색 입력 시
Private Sub txtKeyword_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch = True
        OperationSearch
        isKeyworldSearch = False
    End If
End Sub


'년 선택
Private Sub cboSelectYear_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
        If Me.cboSelectMonth.value = "전체" Then
            '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            '월 선택이 변경되면서 cboSelectMonth_Change()에서 검색 실행
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        '년도를 선택한 경우
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
        End If
        
        OperationSearch
    End If
    
End Sub

'월 선택
Private Sub cboSelectMonth_Change()
    Dim Y As Long: Y = Year(Date)
    Dim M As Long: M = month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.value = "전체" Then
        txtFromDate.value = ""
        txtToDate.value = ""
        
         If Me.cboSelectMonth.value = "전체" Then
             '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            Me.cboSelectMonth.value = "전체"
        End If
    Else
        pos = InStr(Me.cboSelectYear.value, "년")
        If pos <> 0 Then
            Y = Left(Me.cboSelectYear.value, pos - 1)
        End If
    
        If Me.cboSelectMonth.value = "전체" Then
            txtFromDate.value = DateSerial(Y, 1, 1)
            txtToDate.value = DateSerial(Y, 12, 31)
            
            If Me.optAll.value = False Then Me.optAll.value = True Else OperationSearch
        Else
            pos = InStr(Me.cboSelectMonth.value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.value, pos - 1)
                
                txtFromDate.value = DateSerial(Y, M, 1)
                txtToDate.value = DateSerial(Y, M + 1, 0)
            End If
            
            OperationSearch
        End If
    End If
End Sub


Private Sub Worksheet_Change2(ByVal Target As Range)
    If Target.Address = "$L$20" Or Target.Address = "$J$20" Then
        If Target.value = "" Then
            Range("O20").value = 0
        Else
            If Range("$J$20").value <> "" And Range("$L$20").value <> "" Then
                If IsNumeric(Range("$J$20")) Then
                    Range("O20").value = Format(Range("$J$20") * 0.1, "#,##0")
                End If
            End If
        End If
    End If
End Sub

Sub DeleteKeyword1()
    Me.txtKeyword.value = ""
    OperationSearch
End Sub

Sub DeleteKeyword2()
    Me.txtKeyword2.value = ""
    OperationSearch
End Sub

Private Sub Worksheet_Change(ByVal Target As Range)
    Dim cRow, cCol, VAT As Long
    Dim value As Variant
    
    If Target.Cells.count > 1 Then
        If InStr(Target.Address, "$7") = 0 Then
            MsgBox "여러 셀을 한번에 변경할 수 없습니다.", vbInformation, "작업 확인"
            UndoLastAction
        End If
        Exit Sub
    End If
    
    cRow = Target.row
    cCol = Target.Column
    
    If cRow < 7 Or cCol < 4 Or cCol > 16 Or Range("B" & cRow).value = "" Then
        Exit Sub
    End If
    
    Select Case cCol
        Case 4  '등록일자
            If Not IsDate(Target.value) Then
                MsgBox "날짜를 입력하세요.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "등록일자", Target.value
        Case 5  '고정비
            If Target.value <> "고정비" And Target.value <> "" Then
                MsgBox "고정비는 공백이나 고정비 문자열만 가능합니다.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "고정비", Target.value
        Case 6  '수입지출
            If Target.value <> "수입" And Target.value <> "지출" And Target.value <> "" Then
                MsgBox "수입지출은 수입/지출 문자열만 가능합니다.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "수입지출", Target.value
        Case 7  '분류
            Update_Record_Column shtOperation, Range("B" & cRow).value, "분류2", Target.value
        Case 8  '거래처
            Update_Record_Column shtOperation, Range("B" & cRow).value, "거래처", Target.value
        Case 9  '품목
            Update_Record_Column shtOperation, Range("B" & cRow).value, "품목", Target.value
        Case 10  '금액
            If Target.value <> "" And Not IsNumeric(Target.value) Then
                MsgBox "금액은 숫자만 가능합니다.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "금액", Target.value
        Case 11  '명세서
            If Target.value <> "" And Not IsDate(Target.value) Then
                MsgBox "날짜를 입력하세요.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "명세서", Target.value
        Case 12  '계산서
            If Target.value <> "" And Not IsDate(Target.value) Then
                MsgBox "날짜를 입력하세요.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "계산서", Target.value
        Case 13  '결제
            If Target.value <> "" And Not IsDate(Target.value) Then
                MsgBox "날짜를 입력하세요.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "결제", Target.value
        Case 14  '결제수단
            If Target.value <> "현금" And Target.value <> "3970" And Target.value <> "8358" And Target.value <> "" Then
                MsgBox "결제수단은 현금/3970/8358만 가능합니다.", vbInformation, "작업 확인"
                UndoLastAction
                Exit Sub
            End If
            Update_Record_Column shtOperation, Range("B" & cRow).value, "결제수단", Target.value
        Case 15  '부가세
            MsgBox "부가세는 변경할 수 없습니다.", vbInformation, "작업 확인"
            UndoLastAction
            Exit Sub
        Case 16  '체크
            Update_Record_Column shtOperation, Range("B" & cRow).value, "체크", Target.value
    End Select

    Select Case cCol
        Case 10, 12
            '금액, 계산서 입력 시 부가세 등록
            If Range("J" & cRow).value = "" Or Range("L" & cRow).value = "" Then
                VAT = 0
            Else
                If IsNumeric(Range("J" & cRow).value) Then
                    VAT = Format(Range("J" & cRow).value * 0.1, "#,##0")
                Else
                    VAT = 0
                End If
            End If
            
            Application.EnableEvents = False
            Range("O" & cRow).value = VAT
            Application.EnableEvents = True
            Update_Record_Column shtOperation, Range("B" & cRow).value, "부가세", VAT
    End Select
    
End Sub