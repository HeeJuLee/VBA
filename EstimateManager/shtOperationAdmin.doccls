Option Explicit

Dim isKeyworldSearch As Boolean
Dim searchResultCount As Long
Dim isEnd As Boolean
Dim isListInit As Boolean

Private Sub lswOperationList_Click()
    With Me.lswOperationList
        If Not .SelectedItem Is Nothing Then
            Me.txtOperationID.Value = .SelectedItem.Text
            Range("E7") = Format(.SelectedItem.ListSubItems(2), "mm/dd")
            Range("E8") = .SelectedItem.ListSubItems(3)
            Range("E9") = .SelectedItem.ListSubItems(4)
            Range("E10") = .SelectedItem.ListSubItems(5)
            Range("E11") = .SelectedItem.ListSubItems(6)
            Range("E12") = .SelectedItem.ListSubItems(7)
            If IsNumeric(.SelectedItem.ListSubItems(8)) Then
                Range("E13") = CLng(.SelectedItem.ListSubItems(8))
            Else
                Range("E13") = .SelectedItem.ListSubItems(8)
            End If
            Range("E14") = Format(.SelectedItem.ListSubItems(9), "mm/dd")
            Range("E15") = Format(.SelectedItem.ListSubItems(10), "mm/dd")
            Range("E16") = Format(.SelectedItem.ListSubItems(11), "mm/dd")
            If IsNumeric(.SelectedItem.ListSubItems(12)) Then
                Range("E17") = CLng(.SelectedItem.ListSubItems(12))
            Else
                Range("E17") = .SelectedItem.ListSubItems(12)
            End If
        End If
    End With
End Sub

'최초 로드 시
Private Sub Worksheet_Activate()
    Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    
    Me.txtFromDate.Visible = False
    Me.txtToDate.Visible = False
    Me.Image1.Visible = False
    Me.ImageList1.Visible = False
    Me.txtOperationID.Visible = False
    
    isKeyworldSearch = False
    isEnd = False
    
    If isListInit = False Then
        InitializeLswOperationList
        OperationSearch
    End If
End Sub

'운영비 데이터를 검색해서 시트에 출력
Sub OperationSearch()
    Dim db As Variant
    Dim i As Long
    Dim li As ListItem
    Dim income, expense As Long
    
    
    '운영비 데이터를 읽어옴
    db = Get_DB(shtOperation, False, False)
      
    If Not IsEmpty(db) Then
        If txtFromDate.Value <> "" Then db = Filtered_DB(db, ">=" & txtFromDate.Value, 14)    '등록일자 필드와 날짜 비교
    End If
    If Not IsEmpty(db) Then
        If txtToDate.Value <> "" Then db = Filtered_DB(db, "<=" & txtToDate.Value, 14)    '등록일자 필드와 날짜 비교
    End If
    
    If Not IsEmpty(db) Then
        If Me.txtKeyword.Value <> "" Then db = Filtered_DB(db, Me.txtKeyword.Value)     '모든 필드에서 키워드 검색
    End If
    
    '기존 검색결과 지우기
    ClearOperationSearchResult
    
    '검색결과를 화면에 출력
    If IsEmpty(db) Then
        searchResultCount = 0     '검색결과 건수는 0
    Else
        '검색결과 건수를 저장
        searchResultCount = UBound(db, 1)
        
        income = 0
        expense = 0
        With Me.lswOperationList
            For i = 1 To UBound(db)
               
                Set li = .ListItems.Add(, , db(i, 1))
                li.ListSubItems.Add , , i
                li.ListSubItems.Add , , db(i, 14)
                li.ListSubItems.Add , , db(i, 15)
                li.ListSubItems.Add , , db(i, 3)
                li.ListSubItems.Add , , db(i, 5)
                li.ListSubItems.Add , , db(i, 7)
                li.ListSubItems.Add , , db(i, 8)
                li.ListSubItems.Add , , Format(db(i, 9), "#,##0")
                li.ListSubItems.Add , , db(i, 10)
                li.ListSubItems.Add , , db(i, 11)
                li.ListSubItems.Add , , db(i, 12)
                li.ListSubItems.Add , , Format(db(i, 13), "#,##0")
                
                If db(i, 3) = "수입" Then
                    income = income + db(i, 9)
                ElseIf db(i, 3) = "지출" Then
                    expense = expense + db(i, 9)
                End If
            Next
        End With
    End If
    
    '합계 출력
'    shtOperationAdmin.Range("S4").Value = income
'    shtOperationAdmin.Range("V4").Value = expense
    
    '검색 결과 건수 출력
    Me.lblSearchResult.Caption = searchResultCount & "건"
    
    If isKeyworldSearch = True Then
        Me.txtKeyword.Activate
    End If
    
    isEnd = False
    
End Sub

Sub SetExpenseSummary(db)
    Dim i As Long
    Dim income, expense As Long
    
    income = 0
    expense = 0
    For i = 1 To UBound(db)
        If db(i, 3) = "수입" Then
            income = income + db(i, 9)
        ElseIf db(i, 3) = "지출" Then
            expense = expense + db(i, 9)
        End If
    Next
    
    shtOperationAdmin.Range("Q5").Value = income
    shtOperationAdmin.Range("T5").Value = expense
    
End Sub

'발주 검색결과를 화면에서 지움
Sub ClearOperationSearchResult()
    
    '리스트뷰 내용 삭제
    Me.lswOperationList.ListItems.Clear
    
    '등록 셀 내용 지움
    ClearInputData
    
End Sub

Sub ClearInputData()
    Me.txtOperationID.Value = ""
    Range("E7:E17") = ""
End Sub

Sub InitializeLswOperationList()
    Dim db As Variant
    Dim i, j, totalCost As Long
    Dim li As ListItem
    Dim fullPath As String
    
    fullPath = getLocalFullName(ThisWorkbook.Path) & "\listview-1.jpg"
    
    With Me.ImageList1.ListImages
        .Add , , Me.Image1.Picture
    End With
    
     '리스트뷰 값 설정
    With Me.lswOperationList
        .View = lvwReport
        .Gridlines = True
        .FullRowSelect = True
        .HideColumnHeaders = False
        .HideSelection = True
        .FullRowSelect = True
        .MultiSelect = False
        .LabelEdit = lvwManual
        .SmallIcons = Me.ImageList1
        
        .ColumnHeaders.Clear

        .ColumnHeaders.Add , , "ID", 0
        .ColumnHeaders.Add , , "No", 40, lvwColumnCenter
        .ColumnHeaders.Add , , "등록일자", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "고정비", 60, lvwColumnCenter
        .ColumnHeaders.Add , , "수입지출", 70, lvwColumnCenter
        .ColumnHeaders.Add , , "분류", 50, lvwColumnCenter
        .ColumnHeaders.Add , , "거래처", 100
        .ColumnHeaders.Add , , "품목", 250
        .ColumnHeaders.Add , , "금액", 80, lvwColumnRight
        .ColumnHeaders.Add , , "명세서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "계산서", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "결제", 80, lvwColumnCenter
        .ColumnHeaders.Add , , "부가세", 80, lvwColumnRight
    End With
    
    isListInit = True
End Sub

'발주삭제
Sub OperationDelete()
    Dim cRow As Long
    Dim YN As VbMsgBoxResult
    
    '선택한 행 번호
    cRow = Selection.row

    '데이터가 있는 행이 아닐 경우는 중지
    If cRow < 6 Or Range("B" & cRow).Value = "" Then
        MsgBox "삭제할 행을 선택하신 후 '삭제' 버튼을 클릭하세요."
        End
    End If

    '안내 문구 출력
    YN = MsgBox("선택한 발주정보를 정말로 삭제하시겠습니까?" & vbCrLf & "삭제한 정보는 복구가 불가능합니다.", vbYesNo)
    If YN = vbNo Then Exit Sub
    
    'DB에서 견적정보 삭제
    Delete_Record shtOperation, Range("B" & cRow).Value

    OperationSearch
End Sub

''조회 기간', '검색어' 등 일반 텍스트 박스 클릭 시 Object 선택이 안되도록 클릭이벤트를 빈 매크로로 연결
Sub EmptyProcedure()

End Sub


Sub GoToEnd()
    With Me.lswOperationList
        If .ListItems.count > 0 Then
            If isEnd = False Then
                .SelectedItem = .ListItems(.ListItems.count)
                .SelectedItem.EnsureVisible
                isEnd = True
            Else
                .SelectedItem = .ListItems(1)
                .SelectedItem.EnsureVisible
                isEnd = False
            End If
        End If
    End With
    
    lswOperationList_Click
End Sub

'전체 선택 시 '월'만 전체로 바꿈
Private Sub optAll_Click()
    If Me.cboSelectMonth.Value = "전체" Then
        OperationSearch
    Else
        Me.cboSelectMonth.Value = "전체"
    End If
    
End Sub

'조회기간 - 이번달
Private Sub optThisMonth_Click()
    Dim y As Long: y = Year(Date)
    Dim M As Long: M = Month(Date)
    
    txtFromDate.Value = DateSerial(y, M, 1)
    txtToDate.Value = DateSerial(y, M + 1, 0)
    
    Me.cboSelectYear.Value = y & "년"
    Me.cboSelectMonth.Value = M & "월"      '실제 검색은 cboSelectMonth_change()에서 검색 됨
    
End Sub

'조회기간 - 이전달
Private Sub optLastMonth_Click()
    Dim y As Long: y = Year(Date)
    Dim M As Long: M = Month(Date)
    
    txtFromDate.Value = DateSerial(y, M - 1, 1)
    txtToDate.Value = DateSerial(y, M, 0)
    
    If M = 1 Then
        Me.cboSelectYear.Value = y - 1 & "년"
        Me.cboSelectMonth.Value = "12월"
    Else
        Me.cboSelectYear.Value = y & "년"
        Me.cboSelectMonth.Value = M - 1 & "월"
    End If
    
End Sub

Private Sub cboSelectMonth_DropButtonClick()
    If Me.cboSelectMonth.LineCount = 0 Then
        Me.cboSelectMonth.List = Array("전체", "1월", "2월", "3월", "4월", "5월", "6월", "7월", "8월", "9월", "10월", "11월", "12월")
    End If
End Sub

Private Sub cboSelectYear_DropButtonClick()
    If Me.cboSelectYear.ListCount = 0 Then
        Me.cboSelectYear.List = Array("전체", "2021년", "2020년", "2019년", "2018년", "2017년", "2016년", "2015년", "2014년", "2013년", "2012년", "2011년", "2010년", "2009년", "2008년", "2007년", "2006년", "2005년")
    End If
End Sub


'키워드 검색 입력 시
Private Sub txtKeyword_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    '엔터키 눌려질 경우에만 검색
    If KeyCode = 13 Then
        isKeyworldSearch = True
        OperationSearch
        isKeyworldSearch = False
    End If
End Sub


'년 선택
Private Sub cboSelectYear_Change()
    Dim y As Long: y = Year(Date)
    Dim M As Long: M = Month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.Value = "전체" Then
        txtFromDate.Value = ""
        txtToDate.Value = ""
        
        If Me.cboSelectMonth.Value = "전체" Then
            '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            '월 선택이 변경되면서 cboSelectMonth_Change()에서 검색 실행
            Me.cboSelectMonth.Value = "전체"
        End If
    Else
        '년도를 선택한 경우
        pos = InStr(Me.cboSelectYear.Value, "년")
        If pos <> 0 Then
            y = Left(Me.cboSelectYear.Value, pos - 1)
        End If
    
        If Me.cboSelectMonth.Value = "전체" Then
            txtFromDate.Value = DateSerial(y, 1, 1)
            txtToDate.Value = DateSerial(y, 12, 31)
        Else
            pos = InStr(Me.cboSelectMonth.Value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.Value, pos - 1)
                
                txtFromDate.Value = DateSerial(y, M, 1)
                txtToDate.Value = DateSerial(y, M + 1, 0)
            End If
        End If
        
        OperationSearch
    End If
    
End Sub

'월 선택
Private Sub cboSelectMonth_Change()
    Dim y As Long: y = Year(Date)
    Dim M As Long: M = Month(Date)
    Dim pos As Long
    
    If Me.cboSelectYear.Value = "전체" Then
        txtFromDate.Value = ""
        txtToDate.Value = ""
        
         If Me.cboSelectMonth.Value = "전체" Then
             '월 선택이 전체로 선택되어 있으면 바로 검색 실행
            OperationSearch
        Else
            Me.cboSelectMonth.Value = "전체"
        End If
    Else
        pos = InStr(Me.cboSelectYear.Value, "년")
        If pos <> 0 Then
            y = Left(Me.cboSelectYear.Value, pos - 1)
        End If
    
        If Me.cboSelectMonth.Value = "전체" Then
            txtFromDate.Value = DateSerial(y, 1, 1)
            txtToDate.Value = DateSerial(y, 12, 31)
            
            If Me.optAll.Value = False Then Me.optAll.Value = True Else OperationSearch
        Else
            pos = InStr(Me.cboSelectMonth.Value, "월")
            If pos <> 0 Then
                M = Left(Me.cboSelectMonth.Value, pos - 1)
                
                txtFromDate.Value = DateSerial(y, M, 1)
                txtToDate.Value = DateSerial(y, M + 1, 0)
            End If
            
            OperationSearch
        End If
    End If
End Sub

